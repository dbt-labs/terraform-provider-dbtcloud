---
page_title: "dbtcloud_repository Resource - dbtcloud"
subcategory: ""
description: |-
  
---

# dbtcloud_repository (Resource)

*Note*: Some upstream resources can be slow to create, so if creating a project at
the same time as the repository, it's recommended to use the `depends_on` meta argument.

In order to find the `github_installation_id`, you can log in to dbt Cloud, replace `<dbt_cloud_url>` by your dbt Cloud
URL and run the following commands in the Google Chrome console:

```console
dbt_cloud_api_result = await (fetch('https://<dbt_cloud_url>/api/v2/integrations/github/installations/').then(res => res.json()));
console.log("github_application_id: " + dbt_cloud_api_result.filter(res => res["access_tokens_url"].includes("github"))[0]["id"]);
```

Alternatively, you can go to the page `https://<dbt_cloud_url>/api/v2/integrations/github/installations/` and read the
value of `id`  or use the `http` provider to retrieve it automatically like in the example below.

## Example Usage

```terraform
// NOTE for customers using the LEGACY dbt_cloud provider:
// use dbt_cloud_repository instead of dbtcloud_repository for the legacy resource names
// legacy names will be removed from 0.3 onwards


### repo cloned via the GitHub integration, manually entering the `github_installation_id`
resource "dbtcloud_repository" "github_repo" {
  project_id             = dbtcloud_project.dbt_project.id
  remote_url             = "git@github.com:<github_org>/<github_repo>.git"
  github_installation_id = 9876
  git_clone_strategy     = "github_app"
}


### repo cloned via the GitHub integration, with auto-retrieval of the `github_installation_id`
# here, we assume that `token` and `host_url` are respectively accessible via `var.dbt_token` and `var.dbt_host_url`
# NOTE: the following requires connecting via a user token and can't be retrieved with a service token
data "http" "github_installations_response" {
  url = format("%s/v2/integrations/github/installations/", var.dbt_host_url)
  request_headers = {
    Authorization = format("Bearer %s", var.dbt_token)
  }
}

locals {
  github_installation_id = jsondecode(data.http.github_installations_response.response_body)[0].id
}

resource "dbtcloud_repository" "github_repo_other" {
  project_id             = dbtcloud_project.dbt_project.id
  remote_url             = "git@github.com:<github_org>/<github_repo>.git"
  github_installation_id = local.github_installation_id
  git_clone_strategy     = "github_app"
}


### repo cloned via the GitLab integration
# as of 15 Sept 2023 this resource requires using a user token and can't be set with a service token - CC-791
resource "dbtcloud_repository" "gitlab_repo" {
  project_id         = dbtcloud_project.dbt_project.id
  remote_url         = "<gitlab-group>/<gitlab-project>"
  gitlab_project_id  = 8765
  git_clone_strategy = "deploy_token"
}


### repo cloned via the deploy token strategy
resource "dbtcloud_repository" "deploy_repo" {
  project_id         = dbtcloud_project.dbt_project.id
  remote_url         = "git://github.com/<github_org>/<github_repo>.git"
  git_clone_strategy = "deploy_key"
}


### repo cloned via the Azure Dev Ops integration
resource "dbtcloud_repository" "ado_repo" {
  project_id = dbtcloud_project.dbt_project.id
  # the following values can be added manually (IDs can be retrieved from the ADO API) or via data sources
  # remote_url                              = "https://abc@dev.azure.com/abc/def/_git/my_repo"
  # azure_active_directory_project_id       = "12345678-1234-1234-1234-1234567890ab"
  # azure_active_directory_repository_id    = "87654321-4321-abcd-abcd-464327678642"
  remote_url                                = data.dbtcloud_azure_dev_ops_repository.my_devops_repo.remote_url
  azure_active_directory_repository_id      = data.dbtcloud_azure_dev_ops_repository.my_devops_repo.id
  azure_active_directory_project_id         = data.dbtcloud_azure_dev_ops_project.my_devops_project.id
  azure_bypass_webhook_registration_failure = false
  git_clone_strategy                        = "azure_active_directory_app"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `project_id` (Number) Project ID to create the repository in
- `remote_url` (String) Git URL for the repository or \<Group>/\<Project> for Gitlab

### Optional

- `azure_active_directory_project_id` (String) The Azure Dev Ops project ID. It can be retrieved via the Azure API or using the data source `dbtcloud_azure_dev_ops_project` and the project name - (for ADO native integration only)
- `azure_active_directory_repository_id` (String) The Azure Dev Ops repository ID. It can be retrieved via the Azure API or using the data source `dbtcloud_azure_dev_ops_repository` along with the ADO Project ID and the repository name - (for ADO native integration only)
- `azure_bypass_webhook_registration_failure` (Boolean) If set to False (the default), the connection will fail if the service user doesn't have access to set webhooks (required for auto-triggering CI jobs). If set to True, the connection will be successful but no automated CI job will be triggered - (for ADO native integration only)
- `fetch_deploy_key` (Boolean, Deprecated) Whether we should return the public deploy key - (for the `deploy_key` strategy)
- `git_clone_strategy` (String) Git clone strategy for the repository. Can be `deploy_key` (default) for cloning via SSH Deploy Key, `github_app` for GitHub native integration, `deploy_token` for the GitLab native integration and `azure_active_directory_app` for ADO native integration
- `github_installation_id` (Number) Identifier for the GitHub App - (for GitHub native integration only)
- `gitlab_project_id` (Number) Identifier for the Gitlab project -  (for GitLab native integration only)
- `is_active` (Boolean) Whether the repository is active

### Read-Only

- `deploy_key` (String) Public key generated by dbt when using `deploy_key` clone strategy
- `id` (String) The ID of this resource.
- `repository_credentials_id` (Number) Credentials ID for the repository (From the repository side not the dbt Cloud ID)
- `repository_id` (Number) Repository Identifier

## Import

Import is supported using the following syntax:

```shell
# Import using a project ID and repository ID found in the URL or via the API.
terraform import dbtcloud_repository.test_repository "project_id:repository_id"
terraform import dbtcloud_repository.test_repository 12345:6789
```
